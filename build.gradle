// Top-level build file where you can add configuration options common to all sub-projects/modules.


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProps = new Properties()
keystoreProps.load(new FileInputStream(keystorePropertiesFile))

//check unset keystore properties
["GOOGLE_MAPS_API_KEY",
 "FITBIT_CLIENT_ID", "FITBIT_CLIENT_SECRET",
 "MISFIT_APP_KEY", "MISFIT_APP_SECRET",
 "RESCUETIME_CLIENT_ID", "RESCUETIME_CLIENT_SECRET", "RESCUETIME_REDIRECT_URI",
 "JAWBONE_CLIENT_ID", "JAWBONE_CLIENT_SECRET", "JAWBONE_REDIRECT_URI"].forEach {
    key ->
        if (!keystoreProps.containsKey(key)) {
            keystoreProps.setProperty(key, "null")
        }
}

//load google-services.json
File googleServicesFile = rootProject.file("google-services.json")
def googleServicesJson = new groovy.json.JsonSlurper().parseText(googleServicesFile.text)

//load buildConfig.json
File buildConfigJsonFile = rootProject.file("omnitrackBuildConfig.json")
def buildConfigs = new groovy.json.JsonSlurper().parseText(buildConfigJsonFile.text)


ext {
    targetSdkVersion = 28
    compileSdkVersion = 28
    buildToolsVersion = '28.0.2'
    minSdkVersion = 19
}

buildscript {
    ext.kotlin_version = '1.2.60'
    ext.google_play_version = '15.0.1'
    ext.android_support_version = '27.1.1'
    ext.dagger_version = '2.16'
    ext.gson_version = '2.8.2'
    ext.glide_version = '4.7.1'
    ext.rxjava_version = '2.1.14'
    ext.retrofit_version = '2.4.0'
    ext.okhttp_version = '3.10.0'
    ext.gradle_version = '3.1.3'

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.gms:google-services:3.1.2'
        classpath "io.realm:realm-gradle-plugin:5.4.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
apply plugin: 'kotlin'

allprojects {
    repositories {
        jcenter()
        google()
    }

    project.ext{
        buildConfigJson = buildConfigs
        keystoreProperties = keystoreProps
        googleServices = googleServicesJson
    }
}
/*
task clean(type: Delete) {
    delete rootProject.buildDir
}*/

repositories {
    mavenCentral()
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
